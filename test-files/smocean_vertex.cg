struct smoutput {
  float4 position : POSITION;
  float4 color : COLOR0;
  float4 params : COLOR1;
  float2 bumpcoord : TEXCOORD0;
  float3 lightdir : TEXCOORD1;
  float3 halfvec : TEXCOORD2;
  float3 ray : TEXCOORD3;
};

void calc_sincos(const in float2 xy,
                 const in float4 geowaveFreq,
                 const in float4 geowavePhase,
                 const in float2 geowave1dir,
                 const in float2 geowave2dir,
                 const in float2 geowave3dir,
                 const in float2 geowave4dir,
                 out float4 sinvec,
                 out float4 cosvec)
{
  float sinval, cosval;

  // FIXME: precalc geowaveFreq * wavedirvector ??
  sincos(dot(xy, geowave1dir) * geowaveFreq[0] + geowavePhase[0], sinval, cosval); 
  sinvec[0] = sinval;
  cosvec[0] = cosval;

  sincos(dot(xy, geowave2dir) * geowaveFreq[1] + geowavePhase[1], sinval, cosval); 
  sinvec[1] = sinval;
  cosvec[1] = cosval;

  sincos(dot(xy, geowave3dir) * geowaveFreq[2] + geowavePhase[2], sinval, cosval); 
  sinvec[2] = sinval;
  cosvec[2] = cosval;

  sincos(dot(xy, geowave4dir) * geowaveFreq[3] + geowavePhase[3], sinval, cosval); 
  sinvec[3] = sinval;
  cosvec[3] = cosval;
} 


float4 calc_position(const in float2 xy,
                     const in float4 geowaveAmp,     
                     const in float2 geowave1dir,
                     const in float2 geowave2dir,
                     const in float2 geowave3dir,
                     const in float2 geowave4dir,
                     const in float4 geowaveQ,
                     const in float4 sinvec,
                     const in float4 cosvec)
{
  float4 ampQ = geowaveQ * geowaveAmp;

  // FIXME: precalculate these vectors outside the program and pass them as parameters
  float4 posvecx = float4(ampQ[0] * geowave1dir[0],
                          ampQ[1] * geowave2dir[0],
                          ampQ[2] * geowave3dir[0],
                          ampQ[3] * geowave4dir[0]);
  float4 posvecy = float4(ampQ[0] * geowave1dir[1],
                          ampQ[1] * geowave2dir[1],
                          ampQ[2] * geowave3dir[1],
                          ampQ[3] * geowave4dir[1]);


  float4 p;
  p[0] = xy[0] + dot(posvecx, cosvec);
  p[1] = xy[1] + dot(posvecy, cosvec);

  p[2] = dot(geowaveAmp, sinvec);
  p[3] = 1.0;
  return p;
}


float3 calc_normal(const in float4 geowaveAmp,     
                   const in float4 geowaveFreq,     
                   const in float2 geowave1dir,
                   const in float2 geowave2dir,
                   const in float2 geowave3dir,
                   const in float2 geowave4dir,
                   const in float4 Q,
                   const in float4 sinvec,
                   const in float4 cosvec)
{

  float3 n;

  // FIXME: precaluculate these vectors outside the program
  float4 freqamp = geowaveAmp * geowaveFreq;
  float4 dirx = float4(geowave1dir[0], geowave2dir[0], geowave3dir[0], geowave4dir[0]);
  float4 diry = float4(geowave1dir[1], geowave2dir[1], geowave3dir[1], geowave4dir[1]);

  float4 freqampx = freqamp * dirx;
  float4 freqampy = freqamp * diry;

  n[0] = - dot(freqampx, cosvec);
  n[1] = - dot(freqampy, cosvec);
  n[2] =  1.0f - (dot(Q, freqamp * sinvec));
  n = normalize(n);
  return n;
}

float3 calc_tangent(const in float4 geowaveAmp,     
                    const in float4 geowaveFreq,     
                    const in float2 geowave1dir,
                    const in float2 geowave2dir,
                    const in float2 geowave3dir,
                    const in float2 geowave4dir,
                    const in float4 Q,
                    const in float4 sinvec,
                    const in float4 cosvec)
{
  float4 freqamp = geowaveAmp * geowaveFreq;
  float4 dirx = float4(geowave1dir[0], geowave2dir[0], geowave3dir[0], geowave4dir[0]);
  float4 diry = float4(geowave1dir[1], geowave2dir[1], geowave3dir[1], geowave4dir[1]);

  float3 t = float3(-dot(Q*dirx*diry*freqamp, sinvec),
                    1.0f - dot(Q*diry*diry*freqamp, sinvec),
                    dot(diry * freqamp, cosvec));
  return t;
}

void main(float4 position : POSITION, 
          out smoutput OUT,
          uniform float3 eyepos,
          uniform float3 lightdir,
          uniform float3 attenuation, 
          uniform float4 geowaveAmp,
          uniform float4 geowaveFreq,
          uniform float4 geowavePhase,
          uniform float2 geowave1dir,
          uniform float2 geowave2dir,
          uniform float2 geowave3dir,
          uniform float2 geowave4dir,
          uniform float4 geowaveQ)
{
  float dist = distance(position.xyz, eyepos);
  float att = 1.0f / (attenuation[0] + attenuation[1] * dist + attenuation[2] * dist * dist);
  float4 amp = geowaveAmp * att;

  float4x4 modelViewProj = glstate.matrix.mvp ;
  float4 sinvec;
  float4 cosvec;

  calc_sincos(position.xy,
              geowaveFreq,  
              geowavePhase,
              geowave1dir,
              geowave2dir,
              geowave3dir,
              geowave4dir,
              sinvec, cosvec);

  float4 p = calc_position(position.xy,
                           amp,
                           geowave1dir,
                           geowave2dir,
                           geowave3dir,
                           geowave4dir,
                           geowaveQ,
                           sinvec,
                           cosvec);

  float3 n = calc_normal(amp,
                         geowaveFreq,
                         geowave1dir,
                         geowave2dir,
                         geowave3dir,
                         geowave4dir,
                         geowaveQ,
                         sinvec, cosvec);

  float3 t = calc_tangent(amp,
                         geowaveFreq,
                         geowave1dir,
                         geowave2dir,
                         geowave3dir,
                         geowave4dir,
                         geowaveQ,
                         sinvec, cosvec);

  float3 binormal = cross(t, n);
  float3x3 rot = float3x3(t, binormal, n);
  
  OUT.bumpcoord = float2(position.x*0.04, position.y*0.04);  

  float3 ldir = mul(rot, lightdir);
  OUT.lightdir = ldir;
  float3 eyedir = mul(rot, normalize(eyepos - p.xyz));
  OUT.halfvec = normalize(normalize(ldir) + normalize(eyedir));

  OUT.position = mul(modelViewProj, p);    // position in clip space
  OUT.color = float4(0.2, 0.4, 0.7, 1);
  OUT.ray = -eyedir;
  OUT.params = float4(max(att, 0.05), 0.0f, 0.0f, 0.0f);
}




